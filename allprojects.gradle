allprojects {
    apply plugin: 'java'
    apply plugin: 'idea'
    apply plugin: 'kotlin'
    apply plugin: 'maven'
    apply plugin: 'com.github.johnrengelman.shadow'
    apply plugin: 'application'

    group = 'com.github.morj.yjp-flamegraphs'
    sourceCompatibility = 1.7
    version = 'dev'

    repositories {
        mavenCentral()
    }

    tasks.withType(Test) {
        testLogging.showStandardStreams = true
        outputs.upToDateWhen { false }

        System.getProperties().keySet().findAll {it.startsWith('test.')}.each {
            jvmArgs "-D${it.replace('test.','')}=${System.getProperty(it)}"

        }
    }

    task sourcesJar(type: Jar, dependsOn: classes) {
        classifier = 'sources'
        from sourceSets.main.allSource
    }

    idea {
        module { downloadSources = true }
    }

    artifacts {
        archives jar
        archives sourcesJar
    }

    mainClassName = "com.github.morj.flamegraphs.MainKt"

    shadowJar {
        mergeServiceFiles()
        exclude 'META-INF/*.DSA'
        exclude 'META-INF/*.RSA'
    }

    jar {
        manifest {
            attributes 'Main-Class': mainClassName
        }
    }
}
